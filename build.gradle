plugins {
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.2.61'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'org.springframework.boot' version '2.0.5.RELEASE'
    id 'project-report'
}

repositories {
    mavenCentral()
}

configurations.all {
    exclude module: 'junit'
}

dependencyManagement {
    dependencies {
        dependencySet(group:'org.junit.jupiter', version: '5.1.0') {
            entry 'junit-jupiter-api'
            entry 'junit-jupiter-engine'
        }
    }
}

dependencies {
    // Kotlin
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    // Spring
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-devtools'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
    // JUnit 5
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    // Enabling JUnit Platform
    // https://docs.gradle.org/current/userguide/java_testing.html#using_junit5
    useJUnitPlatform()
}

// https://docs.spring.io/spring-boot/docs/2.0.5.RELEASE/gradle-plugin/reference/html/#integrating-with-actuator-build-info
springBoot {
	buildInfo()
}

// https://docs.spring.io/spring-boot/docs/2.0.5.RELEASE/gradle-plugin/reference/html/#running-your-application-reloading-resources
bootRun {
	sourceResources sourceSets.main
}
